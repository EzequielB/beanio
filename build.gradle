/**
 * Gradle build script.
 *
 * Custom gradle tasks:
 *   zip     - Builds the BeanIO zip distribution
 *   site    - Builds the BeanIO site distribution
 *
 * See the following URL for instructions regarding deployment to the Sonatype Maven repo:
 *   https://docs.sonatype.org/display/Repository/Sonatype+OSS+Maven+Repository+Usage+Guide
 *
 * BeanIO 3.x and up is built using Java 11, with Java 7 source/target compatibility.
 *
 * To execute the 'uploadArchives' task, the following properties must be specified
 * in an external 'gradle.properties' file:
 *   - (signing properties, see http://www.gradle.org/docs/current/userguide/signing_plugin.html)
 *   - sonatypeUsername
 *   - sonatypePassword
 */

plugins {
    id 'java'
    id 'groovy'

    id 'maven'
    id 'signing'

    id "org.sonarqube" version "3.1.1"
    id 'jacoco'
}

version = "3.0.0.SNAPSHOT"
group = "com.github.beanio"
sourceCompatibility = 1.7
targetCompatibility = 1.7

ext.artifactId = "beanio"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.14'
    testRuntimeOnly group: 'commons-logging', name: 'commons-logging', version: '1.2'
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'src'
            exclude '**/*.java'
            exclude '**/package.html'
        }
    }
    test {
        java {
            srcDir 'test'
            include '**/*.java'
        }
        groovy {
            srcDir 'test'
            include '**/*.groovy'
        }
        resources {
            srcDir 'test'
            exclude '**/*.java'
            exclude '**/*.groovy'
        }
    }
}

javadoc {
    options.header = "BeanIO $version"
    options.docTitle = "<h2>BeanIO 3.0 API</h2>"
    options.footer = "<i>Copyright &copy; 2010-2021 Kevin Seim and BeanIO contributors</i>"
    options.links = [
        "http://download.oracle.com/javase/7/docs/api/"
    ]
}

jar {
    archiveBaseName = "$artifactId"
    includeEmptyDirs = false

    // create a list of exported packages for OSGi
    ant.dirset(id:"osgi.dirs", dir:"src/org/beanio", excludes: "xsd/**")
    ant.pathconvert(refid:"osgi.dirs", property:"osgi.packages", dirsep:".", pathsep:";version=\"${version}\",") {
        map(from:"$basedir/src/", to:"")
    }
    manifest {
        attributes(
            "Implementation-Title": "BeanIO ${version}",
            "Implementation-Version": version,
            "Bundle-ManifestVersion": "2",
            "Bundle-Name": "BeanIO",
            "Bundle-SymbolicName": "com.github.beanio",
            "Bundle-Version": version,
            "Bundle-Vendor": "Kevin Seim",
            "Bundle-Description": "BeanIO OSGi support",
            "Bundle-License": "http://www.apache.org/licenses/LICENSE-2.0",
            "Bundle-DocURL": "https://beanio.github.io",
            "Export-Package": ant.properties["osgi.packages"] + ";version=\"${version}\"",
            "DynamicImport-Package": "*")
    }
    doLast {
        manifest.writeTo("$buildDir/tmp/jar/MANIFEST.MF")
    }
}

test {
    jvmArgs = ["-Duser.country=US", "-Duser.language=en"]
}

task sourcesJar(type: Jar, dependsOn:classes) {
    baseName = "$artifactId"
    classifier = 'sources'
    from("$projectDir/src") {
        exclude '**/package.html'
    }
    manifest {
        attributes(
            "Implementation-Title": "BeanIO ${version}",
            "Implementation-Version": version
        )
    }
    metaInf {
        from("$projectDir") {
            include "LICENSE.txt"
            include "NOTICE.txt"
        }
    }
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    baseName = "$artifactId"
    classifier = 'javadoc'
    from javadoc.destinationDir
    manifest {
        attributes(
            "Implementation-Title": "BeanIO ${version}",
            "Implementation-Version": version
        )
    }
    metaInf {
        from("$projectDir") {
            include "LICENSE.txt"
            include "NOTICE.txt"
        }
    }
}

task zip(type: Zip, dependsOn: [javadoc, jar] ) {
    baseName = "$artifactId"
    from jar.archivePath
    from(javadoc.destinationDir) {
        into "docs/api"
    }
    from("$projectDir") {
        include "docs/**"
        include "src/**"
        include "test/**"
        include "*.txt"
        include "*.xml"
        include "*.properties"
        include "build.gradle"
    }
}

task site(type: Tar, dependsOn: javadoc) {
    baseName = "$artifactId"
    classifier "site"
    version ""
    compression Compression.GZIP
    from(javadoc.destinationDir) {
        into "3.0/docs/api"
    }
    from ("$projectDir/docs") {
        into "3.0/docs"
    }
    from("$projectDir/site")
    from("$projectDir/src/org/beanio/xsd") {
        include "2012/**"
    }
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
               name 'BeanIO'
               artifactId "$artifactId"
               packaging 'jar'
               description 'A Java un/marshalling library for CSV, XML, delimited and fixed length stream formats.'
               url 'http://beanio.org/'
               scm {
                   url 'http://beanio.googlecode.com/svn'
                   connection 'scm:svn:http://beanio.googlecode.com/svn/trunk'
                   developerConnection 'scm:svn:https://beanio.googlecode.com/svn/trunk'
               }
               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }
               developers {
                   developer {
                       id 'kevinseim'
                       name 'Kevin Seim'
                   }
               }
            }
            pom.whenConfigured {
                dependencies.find { dep -> dep.artifactId == 'commons-logging' }.optional = true
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "beanio"
        property "sonar.organization", "beanio"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
